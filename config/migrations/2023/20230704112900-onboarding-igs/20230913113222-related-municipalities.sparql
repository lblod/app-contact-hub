INSERT {
  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    ?municipality <http://www.w3.org/ns/org#hasSubOrganization> ?bestuur .
  }
} WHERE {
  VALUES ?classificationIgs {
    <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/b156b67f-c5f4-4584-9b30-4c090be02fdc>
    <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/d01bb1f6-2439-4e33-9c25-1fc295de2e71>
    <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/cd93f147-3ece-4308-acab-5c5ada3ec63d>
    <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/4b8450cf-a326-4c66-9e63-b4ec10acc7f6>
  }
  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    ?bestuur a <http://data.vlaanderen.be/ns/besluit#Bestuurseenheid> ;
      <http://www.w3.org/ns/org#classification> ?classificationIgs ;
      <http://www.w3.org/ns/org#hasPrimarySite>/<https://data.vlaanderen.be/ns/organisatie#bestaatUit> ?address .

    ?address <https://data.vlaanderen.be/ns/adres#gemeentenaam> ?municipalityLabel .
  }

  GRAPH <http://mu.semte.ch/graphs/shared> {
    ?municipality a <http://data.vlaanderen.be/ns/besluit#Bestuurseenheid> ;
      skos:prefLabel ?municipalityLabel ;
      <http://www.w3.org/ns/org#classification> <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000001> .
  }
}
