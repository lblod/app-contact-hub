PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>

DELETE {
  GRAPH ?g {
    ?organization org:hasSubOrganization ?subOrganization.
  }
}
INSERT {
  GRAPH ?g {
    ?subOrganization org:memberOf ?organization.

    ?membership
      org:organization ?organization;
      org:member ?subOrganization;
      org:role <http://data.lblod.info/id/rollen/4ec7d5c39bdc4e84b4174379b9e22ad8>.
  }
}
WHERE {
    GRAPH ?g {
      ?organization org:hasSubOrganization ?subOrganization.
    }
  BIND(URI(CONCAT("http://data.lblod.info/id/lidmaatschap/", SHA1(CONCAT(STR(?organization), STR(?subOrganization))))) AS ?membership)
}