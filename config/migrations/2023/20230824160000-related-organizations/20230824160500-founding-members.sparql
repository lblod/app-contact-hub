PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>

DELETE {
  GRAPH ?g {
    ?foundingOrg <http://mu.semte.ch/vocabularies/ext/isFounderOfOrganization> ?foundedOrg.
  }
}
INSERT {
  GRAPH ?g {
    ?foundingOrg org:hasMembership ?membership.

    ?membership
      org:organization ?foundedOrg;
      org:member ?foundingOrg;
      org:role <http://data.lblod.info/id/rollen/73d5e1cf250d42fab15926771f07505a>.
  }
}
WHERE {
    GRAPH ?g {
      ?foundingOrg <http://mu.semte.ch/vocabularies/ext/isFounderOfOrganization> ?foundedOrg.
    }
  BIND(URI(CONCAT("http://data.lblod.info/id/lidmaatschap/", SHA1(CONCAT(STR(?foundingOrg), STR(?foundedOrg))))) AS ?membership)
}