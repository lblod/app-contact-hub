PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX session: <http://mu.semte.ch/vocabularies/session/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>


DELETE {
  GRAPH <http://mu.semte.ch/graphs/organisatieportaal> {
      ?s ?p ?o
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/worship-service> {
    ?s ?p ?o
  }
}

WHERE {
  GRAPH <http://mu.semte.ch/graphs/organisatieportaal> {
    ?s a ?type; ?p ?o.
    
    filter (?type in (
                   ere:EredienstMandataris,
                       ere:BestuurVanDeEredienst,
                       ere:PositieBedienaar,
                       ere:BetrokkenLokaleBesturen,
                       ere:VerbondenJuridischeStructuren,
                       ere:RolBedienaar,
                       ere:EredienstBestuurseenheid,
                       ere:VoorwaardenBedienaar,
                       ere:CentraalBestuurVanDeEredienst,
                       ere:RepresentatiefOrgaan
    ))

  }
}
;
  
DELETE {
  GRAPH <http://mu.semte.ch/graphs/organisatieportaal> {
    ?governingBody ?p ?o.
    ?specialization ?specializationP ?specializationO.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/worship-service> {
    ?governingBody ?p ?o.
    ?specialization ?specializationP ?specializationO.
  }
}

WHERE {
  GRAPH <http://mu.semte.ch/graphs/organisatieportaal> {
     ?governingBody generiek:isTijdspecialisatieVan ?specialization;
     ?p ?o.
      ?specialization <http://www.w3.org/ns/org#classification> ?classification;
                     ?specializationP ?specializationO.
      
            ?classification <http://mu.semte.ch/vocabularies/ext/appliesWithin> ?appliesWithin.
             ?appliesWithin mu:uuid ?classificationUuid.

    filter(?classificationUuid in ("66ec74fd-8cfc-4e16-99c6-350b35012e86","f9cac08a-13c1-49da-9bcb-f650b0604054"))
}


}
;
  
DELETE {
  GRAPH <http://mu.semte.ch/graphs/organisatieportaal> {
    ?mandaat ?p ?o.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/worship-service> {
     ?mandaat ?p ?o.
  }
}

WHERE {
  GRAPH <http://mu.semte.ch/graphs/organisatieportaal> {
      ?mandaat ?p ?o.
      filter exists {
         GRAPH <http://mu.semte.ch/graphs/worship-service> {
             ?orgaan <http://www.w3.org/ns/org#hasPost> ?mandaat.
         
          }
      }
}


}
;

DELETE {
  GRAPH <http://mu.semte.ch/graphs/organisatieportaal> {
  ?s ?p ?o.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/worship-service> {
   ?s ?p ?o.
   }
}

WHERE {
  GRAPH <http://mu.semte.ch/graphs/organisatieportaal> {
  ?s a  <http://data.vlaanderen.be/ns/besluit#Bestuursorgaan> ;
      <https://data.vlaanderen.be/ns/generiek#isTijdspecialisatieVan> ?specialization;
                     ?p ?o.
      
         filter exists {
           GRAPH <http://mu.semte.ch/graphs/worship-service> {
              ?specialization a <http://data.vlaanderen.be/ns/besluit#Bestuursorgaan>.
            }
         }
}


}
