PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX session: <http://mu.semte.ch/vocabularies/session/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek>

INSERT  {
  graph <http://mu.semte.ch/graphs/worship-service> {
     ?person a person:Person;
             mu:uuid ?uuid;
             foaf:givenName ?givenName;
             foaf:familyName ?familyName;
             <http://data.vlaanderen.be/ns/mandaat#isAangesteldAls> ?isAangesteldAls;
             persoon:gebruikteVoornaam ?firstNameUsed.
  }

}
WHERE {
  graph <http://mu.semte.ch/graphs/organisatieportaal> {
     ?person a person:Person;
             mu:uuid ?uuid;
             foaf:givenName ?givenName;
             foaf:familyName ?familyName.
      optional {?person  persoon:gebruikteVoornaam ?firstNameUsed. }
      optional {?person   <http://data.vlaanderen.be/ns/mandaat#isAangesteldAls> ?isAangesteldAls.}
    
    filter (exists
      {
        graph <http://mu.semte.ch/graphs/worship-service> {
           ?mandatories <http://data.vlaanderen.be/ns/mandaat#isBestuurlijkeAliasVan> ?person.
        }
      } 

     || exists {
        graph <http://mu.semte.ch/graphs/worship-service> {
          ?ministers <http://www.w3.org/ns/org#heldBy> ?person.
      }
    })

  } 
}
;

# delete orphans
DELETE {
  graph <http://mu.semte.ch/graphs/organisatieportaal> {
     ?person a person:Person;
             foaf:givenName ?givenName;
             foaf:familyName ?familyName;
            <http://data.vlaanderen.be/ns/mandaat#isAangesteldAls> ?isAangesteldAls;
             persoon:gebruikteVoornaam ?firstNameUsed.
  }

}
WHERE {
  graph <http://mu.semte.ch/graphs/organisatieportaal> {
     ?person a person:Person;
             foaf:givenName ?givenName;
             foaf:familyName ?familyName.
      optional{?person persoon:gebruikteVoornaam ?firstNameUsed.}
      optional {?person   <http://data.vlaanderen.be/ns/mandaat#isAangesteldAls> ?isAangesteldAls.}

    FILTER((NOT EXISTS { ?mandatories <http://data.vlaanderen.be/ns/mandaat#isBestuurlijkeAliasVan> ?person. })
    && (NOT EXISTS { ?ministers <http://www.w3.org/ns/org#heldBy> ?person. }))

  } 
}


