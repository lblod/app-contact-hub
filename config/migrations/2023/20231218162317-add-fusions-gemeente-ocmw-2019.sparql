PREFIX org: <http://www.w3.org/ns/org#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ch: <http://data.lblod.info/vocabularies/contacthub/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    ?changeEvent a org:ChangeEvent ;
      mu:uuid ?uuidChangeEvent ;
      ch:typeWijziging <http://lblod.data.gift/concepts/fa80032794c841ecac73ff874f856db1> ;
      org:resultingOrganization ?resultingOrg ;
      dct:date "2019-01-01"^^xsd:date ;
      dct:description ?description ;
      org:originalOrganization ?firstOrgToMerge ;
      org:originalOrganization ?secondOrgToMerge ;
      lblodOrg:veranderingsgebeurtenisResultaat ?changeEventFirstNotActiveResult ;
      lblodOrg:veranderingsgebeurtenisResultaat ?changeEventSecondNotActiveResult ;
      lblodOrg:veranderingsgebeurtenisResultaat ?changeEventActiveResult .

  ?changeEventFirstNotActiveResult a lblodOrg:VeranderingsgebeurtenisResultaat ;
    mu:uuid ?uuidChangeEventFirstNotActiveResult ;
    lblodOrg:resulterendeStatus <http://lblod.data.gift/concepts/d02c4e12bf88d2fdf5123b07f29c9311> ; # not active
    ext:resultingOrganization ?firstOrgToMerge .

  ?changeEventSecondNotActiveResult a lblodOrg:VeranderingsgebeurtenisResultaat ;
    mu:uuid ?uuidChangeEventSecondNotActiveResult ;
    lblodOrg:resulterendeStatus <http://lblod.data.gift/concepts/d02c4e12bf88d2fdf5123b07f29c9311> ; # not active
    ext:resultingOrganization ?secondOrgToMerge .

  ?changeEventActiveResult a lblodOrg:VeranderingsgebeurtenisResultaat ;
    mu:uuid ?uuidChangeEventActiveResult ;
    lblodOrg:resulterendeStatus <http://lblod.data.gift/concepts/63cc561de9188d64ba5840a42ae8f0d6> ; # active
    ext:resultingOrganization ?resultingOrg .
  }
} WHERE {
  VALUES (?firstOrgToMerge ?secondOrgToMerge ?resultingOrg) {
    (<http://data.lblod.info/id/bestuurseenheden/5eaaceb01cc2a6bc418d5fcdc6c855bdfe1d1fcbc8abf8bb9cfcea76edbc8936> <http://data.lblod.info/id/bestuurseenheden/be8c097326db7ba0e0e7c81b832da8b4cdeabe22b515494753dd95d33d9ea295> <http://data.lblod.info/id/bestuurseenheden/47857958-7d00-47e0-ae83-00d914eca93f>) # Gemeente Oudsbergen
    (<http://data.lblod.info/id/bestuurseenheden/43ad404aee5fd4f07d575e118883934b0ad0ead0ef8cee0793b7f72e4c1ce19f> <http://data.lblod.info/id/bestuurseenheden/be5f63236aacb5478334077a901dbda6816a7b923753b49c06295777b47e6aa4> <http://data.lblod.info/id/bestuurseenheden/13b25c80-8d3a-4821-95b0-ffc4e850cb4b>) # OCMW Oudsbergen 
    (<http://data.lblod.info/id/bestuurseenheden/d7d9ef5babd2876ad492a71c2019f02cf90c813c00befcf3682ba49186d0f4b1> <http://data.lblod.info/id/bestuurseenheden/e3fc7eb1c6f50aa4277e0ae180b002eddf243dab630e9278996df9951316da68> <http://data.lblod.info/id/bestuurseenheden/8d7de878-db13-4fe1-8d74-bb8c9a690d90>) # Gemeente Pelt
    (<http://data.lblod.info/id/bestuurseenheden/e3289466b88127a445e1467a360f24221bdac2723613b8adabeb131e8d9a3659> <http://data.lblod.info/id/bestuurseenheden/f5077ea4d13ee6d74749b2d9e7de428b0633388a10b436984859021cf62731b9> <http://data.lblod.info/id/bestuurseenheden/fad1ca7a-ac26-43fc-b167-b8ae4f873553>) # OCMW Pelt
    (<http://data.lblod.info/id/bestuurseenheden/571b85dc585067ac92f9de90133e747c75f3f7e7948087ae193583b9f0fe1141> <http://data.lblod.info/id/bestuurseenheden/1b11f7f11c3c74c54acf86b8c1a9b8b2865e22b8973818486045a5d3aa8dff14> <http://data.lblod.info/id/bestuurseenheden/c8f33e80-6f19-4ba1-a758-9d85f42d28d4>) # Gemeente Puurs-Sint-Amands
    (<http://data.lblod.info/id/bestuurseenheden/2712dbaca999e57d26f1f0d3e72b2ed96113d6ac62d72b7228fd682e1324e4e5> <http://data.lblod.info/id/bestuurseenheden/be274f73ffbd75a9cf28222f45044f468b40692ca9c80c8c5899acf554498e24> <http://data.lblod.info/id/bestuurseenheden/a25044d6-d9f9-496e-9061-b0c858dda944>) # OCMW Puurs-Sint-Amands
    (<http://data.lblod.info/id/bestuurseenheden/9b93ff5308590c7d03c97c3b892910f912e695ad45c82465934996fb04dac859> <http://data.lblod.info/id/bestuurseenheden/b85b33fb0ce57e2b332599d2aaafcab35a3560b709add3cec98e21dca6cdc210> <http://data.lblod.info/id/bestuurseenheden/5116efa8-e96e-46a2-aba6-c077e9056a96>) # Gemeente Aalter
    (<http://data.lblod.info/id/bestuurseenheden/c895809729a165666fc111a40affe229f3c8e8f1ce9dec2dcc1be1ed81b52d9b> <http://data.lblod.info/id/bestuurseenheden/92207a4e09455f59f19553e95ebc4ea1b33e6f7b2e5cf317ea31ba7ac0cbca45> <http://data.lblod.info/id/bestuurseenheden/f73c7109-06a0-45d7-b558-879d87ca3553>) # OCMW Aalter
    (<http://data.lblod.info/id/bestuurseenheden/25b05f670144f166a485a1d0ac37faca672b36642cf1647d846010d6fe9cabb5> <http://data.lblod.info/id/bestuurseenheden/8ed14ecd083fb231f9af0f2698de728bba3f11deb0f4ea4caf9d5c1f9e3a771b> <http://data.lblod.info/id/bestuurseenheden/d93451bf-e89a-4528-80f3-f0a1c19361a8>) # Gemeente Deinze
    (<http://data.lblod.info/id/bestuurseenheden/56db39c0bb1c85e9c5aec9adb3f5af9720d4702e30f4772aecd272f03c7becea> <http://data.lblod.info/id/bestuurseenheden/c78f64baa2f264a2c8b5207f788c285ffc1c0393cf20e6fc83ab61548a2baf9c> <http://data.lblod.info/id/bestuurseenheden/a4fd24f3-cc6d-4ea5-91c7-efa02d3b970f>) # OCMW Deinze
    (<http://data.lblod.info/id/bestuurseenheden/db046b577b77c00cc82b51b1cdf14396364cbe1ebc0f4cfee09619bec9b0bd5e> <http://data.lblod.info/id/bestuurseenheden/19537690173d82d774bf34abaa07f3b1af78139014e53aa8f172a0a558bc5e09> <http://data.lblod.info/id/bestuurseenheden/52cc723a-9717-496d-9897-c6774cf124e4>) # Gemeente Kruisem
    (<http://data.lblod.info/id/bestuurseenheden/19673274e30b682be3dd3178352611aa26c28ea8907fd4990ff69e01af7019f6> <http://data.lblod.info/id/bestuurseenheden/cb6ea2dd7a009bca2aa4c1058cce5545420792b9db0a6b5516e49a4ae4b03701> <http://data.lblod.info/id/bestuurseenheden/d26e802b-cae8-4c52-bad6-39e349d46862>) # OCMW Kruisem
  }
  
  VALUES ?g {
    <http://mu.semte.ch/graphs/administrative-unit>
    <http://mu.semte.ch/graphs/shared>
  }

  GRAPH ?g {
    ?firstOrgToMerge skos:prefLabel ?labelFirstOrg .
    ?secondOrgToMerge skos:prefLabel ?labelSecondOrg .
    ?resultingOrg skos:prefLabel ?labelResultingOrg ;
      org:classification ?classification .
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?classification skos:prefLabel ?classificationResultingOrg .
  }

  BIND(CONCAT("Fusie tussen ", ?labelFirstOrg," en ", ?labelSecondOrg, " resulteert in ", ?classificationResultingOrg, " ", ?labelResultingOrg) as ?description)

  BIND(SHA256(CONCAT(STR(?firstOrgToMerge), STR(?secondOrgToMerge), STR(?resultingOrg),"fusieChangeEvent")) AS ?uuidChangeEvent) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/veranderingsgebeurtenissen/", STR(?uuidChangeEvent))) AS ?changeEvent) .

  BIND(SHA256(CONCAT(STR(?firstOrgToMerge),"fusieChangeEventFirstNotActiveResult")) AS ?uuidChangeEventFirstNotActiveResult) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/veranderingsgebeurtenis-resultaten/", STR(?uuidChangeEventFirstNotActiveResult))) AS ?changeEventFirstNotActiveResult) .

  BIND(SHA256(CONCAT(STR(?secondOrgToMerge),"fusieChangeEventSecondNotActiveResult")) AS ?uuidChangeEventSecondNotActiveResult) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/veranderingsgebeurtenis-resultaten/", STR(?uuidChangeEventSecondNotActiveResult))) AS ?changeEventSecondNotActiveResult) .


  BIND(SHA256(CONCAT(STR(?firstOrgToMerge), STR(?secondOrgToMerge), STR(?resultingOrg),"fusieChangeEventActiveResult")) AS ?uuidChangeEventActiveResult) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/veranderingsgebeurtenis-resultaten/", STR(?uuidChangeEventActiveResult))) AS ?changeEventActiveResult) .
}

;

INSERT {
  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    ?changeEvent a org:ChangeEvent ;
      mu:uuid ?uuidChangeEvent ;
      ch:typeWijziging <http://lblod.data.gift/concepts/fa80032794c841ecac73ff874f856db1> ;
      org:resultingOrganization ?resultingOrg ;
      dct:date "2019-01-01"^^xsd:date ;
      dct:description ?description ;
      org:originalOrganization ?firstOrgToMerge ;
      org:originalOrganization ?secondOrgToMerge ;
      org:originalOrganization ?thirdOrgToMerge ;
      lblodOrg:veranderingsgebeurtenisResultaat ?changeEventFirstNotActiveResult ;
      lblodOrg:veranderingsgebeurtenisResultaat ?changeEventSecondNotActiveResult ;
      lblodOrg:veranderingsgebeurtenisResultaat ?changeEventThirdNotActiveResult ;
      lblodOrg:veranderingsgebeurtenisResultaat ?changeEventActiveResult .

  ?changeEventFirstNotActiveResult a lblodOrg:VeranderingsgebeurtenisResultaat ;
    mu:uuid ?uuidChangeEventFirstNotActiveResult ;
    lblodOrg:resulterendeStatus <http://lblod.data.gift/concepts/d02c4e12bf88d2fdf5123b07f29c9311> ; # not active
    ext:resultingOrganization ?firstOrgToMerge .

  ?changeEventSecondNotActiveResult a lblodOrg:VeranderingsgebeurtenisResultaat ;
    mu:uuid ?uuidChangeEventSecondNotActiveResult ;
    lblodOrg:resulterendeStatus <http://lblod.data.gift/concepts/d02c4e12bf88d2fdf5123b07f29c9311> ; # not active
    ext:resultingOrganization ?secondOrgToMerge .

  ?changeEventThirdNotActiveResult a lblodOrg:VeranderingsgebeurtenisResultaat ;
    mu:uuid ?uuidChangeEventThirdNotActiveResult ;
    lblodOrg:resulterendeStatus <http://lblod.data.gift/concepts/d02c4e12bf88d2fdf5123b07f29c9311> ; # not active
    ext:resultingOrganization ?thirdOrgToMerge .

  ?changeEventActiveResult a lblodOrg:VeranderingsgebeurtenisResultaat ;
    mu:uuid ?uuidChangeEventActiveResult ;
    lblodOrg:resulterendeStatus <http://lblod.data.gift/concepts/63cc561de9188d64ba5840a42ae8f0d6> ; # active
    ext:resultingOrganization ?resultingOrg .
  }
} WHERE {
  VALUES (?firstOrgToMerge ?secondOrgToMerge ?thirdOrgToMerge ?resultingOrg) {
    (<http://data.lblod.info/id/bestuurseenheden/1d43739442010a95b1910af237a4f6aa9a26ca6c9c83f646bad44a8181d407c7> <http://data.lblod.info/id/bestuurseenheden/66374f1f46137c3240ba0690b24056a014f9ad5f36ae81e10bc14ad5a6305089> <http://data.lblod.info/id/bestuurseenheden/46a7ee2627bf51959f9a72fdb6f5aec942f5e07484c1bfe7da2fb8a74c830326> <http://data.lblod.info/id/bestuurseenheden/57b1646f-bf4c-4dc4-8bc9-6ec8a6486018>) # Gemeente Lievegem
    (<http://data.lblod.info/id/bestuurseenheden/32d0fce422c04b004e3cf640d3aa06468f8ec8f650d578806642c1097b95d782> <http://data.lblod.info/id/bestuurseenheden/51793f0f16c2b06e9c64cbc141407fcafaa340576c96988971dc0855f032940c> <http://data.lblod.info/id/bestuurseenheden/f879d68d0f66af67aa045b1194cde7dc5d82e3bfe07746cc682be472c9556631> <http://data.lblod.info/id/bestuurseenheden/120d2462-0e97-4636-b8dd-78edc4b2ba14>) # OCMW Lievegem
  }
  
  VALUES ?g {
    <http://mu.semte.ch/graphs/administrative-unit>
    <http://mu.semte.ch/graphs/shared>
  }

  GRAPH ?g {
    ?firstOrgToMerge skos:prefLabel ?labelFirstOrg .
    ?secondOrgToMerge skos:prefLabel ?labelSecondOrg .
    ?thirdOrgToMerge skos:prefLabel ?labelThirdOrg .
    ?resultingOrg skos:prefLabel ?labelResultingOrg ;
      org:classification ?classification .
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?classification skos:prefLabel ?classificationResultingOrg .
  }

  BIND(CONCAT("Fusie tussen ", ?labelFirstOrg, ", ", ?labelSecondOrg," en ", ?labelThirdOrg, " resulteert in ", ?classificationResultingOrg, " ", ?labelResultingOrg) as ?description)

  BIND(SHA256(CONCAT(STR(?firstOrgToMerge), STR(?secondOrgToMerge), STR(?thirdOrgToMerge), STR(?resultingOrg),"fusieChangeEvent")) AS ?uuidChangeEvent) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/veranderingsgebeurtenissen/", STR(?uuidChangeEvent))) AS ?changeEvent) .

  BIND(SHA256(CONCAT(STR(?firstOrgToMerge),"fusieChangeEventFirstNotActiveResult")) AS ?uuidChangeEventFirstNotActiveResult) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/veranderingsgebeurtenis-resultaten/", STR(?uuidChangeEventFirstNotActiveResult))) AS ?changeEventFirstNotActiveResult) .

  BIND(SHA256(CONCAT(STR(?secondOrgToMerge),"fusieChangeEventSecondNotActiveResult")) AS ?uuidChangeEventSecondNotActiveResult) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/veranderingsgebeurtenis-resultaten/", STR(?uuidChangeEventSecondNotActiveResult))) AS ?changeEventSecondNotActiveResult) .

  BIND(SHA256(CONCAT(STR(?thirdOrgToMerge),"fusieChangeEventThirdNotActiveResult")) AS ?uuidChangeEventThirdNotActiveResult) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/veranderingsgebeurtenis-resultaten/", STR(?uuidChangeEventThirdNotActiveResult))) AS ?changeEventThirdNotActiveResult) .

  BIND(SHA256(CONCAT(STR(?resultingOrg),"fusieChangeEventActiveResult")) AS ?uuidChangeEventActiveResult) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/veranderingsgebeurtenis-resultaten/", STR(?uuidChangeEventActiveResult))) AS ?changeEventActiveResult) .
}
