PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/worship-service> {
    ?orgaanInTime a besluit:Bestuursorgaan ;
      mu:uuid ?uuidOrgaanInTime ;
      mandaat:bindingStart "2023-04-01T00:00:00"^^<http://www.w3.org/2001/XMLSchema#dateTime> ;
      mandaat:bindingEinde "2026-03-31T00:00:00"^^<http://www.w3.org/2001/XMLSchema#dateTime> ;
      generiek:isTijdspecialisatieVan ?bestuursorgaan .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/worship-service> {
    VALUES ?type {
      ere:BestuurVanDeEredienst
      ere:CentraalBestuurVanDeEredienst
    }

    ?bestuurseenheid a ?type .

    FILTER NOT EXISTS {
      ?bestuurseenheid <http://www.w3.org/ns/regorg#orgStatus> <http://lblod.data.gift/concepts/d02c4e12bf88d2fdf5123b07f29c9311> .
    }

    ?bestuursorgaan besluit:bestuurt ?bestuurseenheid .

    FILTER NOT EXISTS {
      ?missingOrgaanInTime generiek:isTijdspecialisatieVan ?bestuursorgaan .
      ?missingOrgaanInTime mandaat:bindingStart ?start .
      FILTER CONTAINS(STR(?start), "2023")
    }

    FILTER NOT EXISTS {
      ?missingOrgaanInTime generiek:isTijdspecialisatieVan ?bestuursorgaan .
      ?missingOrgaanInTime mandaat:bindingEinde ?end .
      FILTER CONTAINS(STR(?end), "2026")
    }
  }

  BIND(MD5(CONCAT(?bestuurseenheid, ?bestuursorgaan, "2023-2026")) as ?uuidOrgaanInTime)
  BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", ?uuidOrgaanInTime)) AS ?orgaanInTime)
}
