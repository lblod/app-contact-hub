PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX session: <http://mu.semte.ch/vocabularies/session/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

INSERT {
     graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
       ?person a person:Person; mu:uuid ?uuid.
     }
}
where {
      graph <http://mu.semte.ch/graphs/worship-service> {
           ?person a person:Person;
             mu:uuid ?uuid.
          filter(exists {
              graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
                   ?s ?p ?person.
              }
          } or exists {
                 graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
                   ?person ?x ?y.
              }

          })

      }

}
;
DELETE {
   graph <http://mu.semte.ch/graphs/worship-service> {
     ?person mu:uuid ?uuid.
  }
}
INSERT {
    graph <http://mu.semte.ch/graphs/worship-service> {
     ?person mu:uuid ?realUUID. 
     ?person ext:oldUuid ?uuid.
  }

}
where {
  graph <http://mu.semte.ch/graphs/worship-service> {
     ?person a person:Person;
             mu:uuid ?uuid.
      # https://github.com/openlink/virtuoso-opensource/issues/515#issuecomment-456848368
      OPTIONAL { ?person ext:newUuid ?newUuid } 
      BIND(IF(BOUND(?newUuid), ?newUuid, STRUUID()) as ?realUUID)  

}
}

;

DELETE {
   graph <http://mu.semte.ch/graphs/worship-service> {
     ?person ?wp ?wo.
     ?ws ?wx ?person.
  }
}
INSERT {
    graph <http://mu.semte.ch/graphs/worship-service> {
     ?newPerson ?wp ?wo.
     ?newPerson owl:sameAs ?person.
     ?ws ?wx ?newPerson.
  }

}
where {
  graph <http://mu.semte.ch/graphs/worship-service> {
     ?person a person:Person;
             mu:uuid ?uuid.
             optional {?person ?wp ?wo.}
             optional {
                ?ws ?wx ?person.
             }
         

  BIND(IRI(CONCAT("http://data.lblod.info/id/personen/", ?uuid)) AS ?newPerson)
}
}

;
DELETE {
  graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
     ?person mu:uuid ?uuid.
  }
}
INSERT {
     graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
        ?person mu:uuid ?newUuid.
     }
}
where {
     graph <http://mu.semte.ch/graphs/worship-service> {
         ?newPerson a person:Person; ext:oldUuid ?uuid;
                 mu:uuid ?newUuid.
      }
     graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
         ?person a person:Person; mu:uuid ?uuid.
     }

}



;
DELETE {
  graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
     ?person ?p ?o.
     ?s ?x ?person.
  }
}

INSERT {
     graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
        ?newPerson ?p ?o.
        ?s ?x ?newPerson.
     }
}

where {
     graph <http://mu.semte.ch/graphs/worship-service> {
         ?newPerson a person:Person; mu:uuid ?uuid.
               
     }
     graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
         ?person mu:uuid ?uuid.
         optional {?person ?p ?o}
         optional {?s ?x ?person}
     }

}
;

delete {
  graph <http://mu.semte.ch/graphs/worship-service> {
         ?person ext:uuid ?uuid.
     }

}
where {
  graph <http://mu.semte.ch/graphs/worship-service> {
         ?person ext:uuid ?uuid.
     }


}
;

delete {
  graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
         ?person ext:uuid ?uuid.
     }

}
where {
  graph <http://mu.semte.ch/graphs/worship-privacy-centric-graph> {
         ?person ext:uuid ?uuid.
     }


}
