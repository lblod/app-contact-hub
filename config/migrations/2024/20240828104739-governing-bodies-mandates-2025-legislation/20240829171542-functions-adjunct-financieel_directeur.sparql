PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX lblodlg: <http://data.lblod.info/vocabularies/leidinggevenden/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    ?governingBodyInTime lblodlg:heeftBestuursfunctie ?function .

    ?function a lblodlg:Bestuursfunctie , org:Post ;
      mu:uuid ?functionUuid ;
      org:role ?functionRole .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/administrative-unit> {
    ?governingBodyInTime a besluit:Bestuursorgaan ;
      generiek:isTijdspecialisatieVan ?governingBody ;
      mandaat:bindingStart "2025-01-01T00:00:00"^^xsd:dateTime .

    ?governingBody org:classification <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/ed40469e-3b6f-4f38-99ba-18912ee352b0> . # Adjunct-financieel directeur
  }

  VALUES ?functionRole {
    <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/3200ffc1-bb72-4235-a81c-64aa578b0789> # Adjunct financieel directeur

  }

  BIND(SHA256(CONCAT(STR(?governingBody), STR(?governingBodyInTime), STR(?functionRole), " 2025")) AS ?functionUuid)
  BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursfuncties/", ?functionUuid)) AS ?function)
}
