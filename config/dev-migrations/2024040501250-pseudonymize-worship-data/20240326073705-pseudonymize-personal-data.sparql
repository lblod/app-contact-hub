PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>

DELETE {
  GRAPH ?g {
    ?person foaf:familyName ?fn .
    ?person foaf:givenName ?gn .
    ?person persoon:gebruikteVoornaam ?gvn.
  }
} INSERT {
  GRAPH ?g {
    ?person foaf:givenName ?gn_s .
    ?person foaf:familyName ?fn_s .
    ?person persoon:gebruikteVoornaam ?gvn_s .
  }
} WHERE {
  # Note: UUID is not inserted in the producer graph
  GRAPH <http://mu.semte.ch/graphs/worship-service> {
    ?person a person:Person ;
      mu:uuid ?uuid .
  }

  BIND("25663716512231276038934668311170119609149297699430613635983433414904082203772" AS ?pepper) .
  # Ensure salt is unique per person so that people with the same name result
  # different the pseudonimized values, similar to how salts are used in
  # password hashing
  BIND(SHA256(CONCAT(?pepper, ?uuid)) AS ?salt) .

  GRAPH ?g {
    ?person a person:Person .
    OPTIONAL {
      ?person foaf:givenName ?gn .
      BIND(SHA256(CONCAT(?salt, ?gn)) AS ?gn_s) .
    }
    OPTIONAL {
      ?person foaf:familyName ?fn .
      BIND(SHA256(CONCAT(?salt, ?fn)) AS ?fn_s) .
    }
    OPTIONAL {
      ?person persoon:gebruikteVoornaam ?gvn .
      BIND(SHA256(CONCAT(?salt, ?gvn)) AS ?gvn_s) .
    }
  }
  VALUES ?g {
    <http://mu.semte.ch/graphs/worship-service>
    <http://redpencil.data.gift/id/deltas/producer/organizations>
  }
}
